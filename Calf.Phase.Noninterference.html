<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Calf.Phase.Noninterference</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--rewriting</a> <a id="25" class="Symbol">#-}</a>

<a id="30" class="Comment">-- Theorems about noninterference.</a>

<a id="66" class="Keyword">open</a> <a id="71" class="Keyword">import</a> <a id="78" href="Algebra.Cost.html" class="Module">Algebra.Cost</a>

<a id="92" class="Keyword">module</a> <a id="99" href="Calf.Phase.Noninterference.html" class="Module">Calf.Phase.Noninterference</a> <a id="126" class="Keyword">where</a>

<a id="133" class="Keyword">open</a> <a id="138" class="Keyword">import</a> <a id="145" href="Calf.Prelude.html" class="Module">Calf.Prelude</a>
<a id="158" class="Keyword">open</a> <a id="163" class="Keyword">import</a> <a id="170" href="Calf.CBPV.html" class="Module">Calf.CBPV</a>
<a id="180" class="Keyword">open</a> <a id="185" class="Keyword">import</a> <a id="192" href="Calf.Phase.Core.html" class="Module">Calf.Phase.Core</a>
<a id="208" class="Keyword">open</a> <a id="213" class="Keyword">import</a> <a id="220" href="Calf.Phase.Open.html" class="Module">Calf.Phase.Open</a>
<a id="236" class="Keyword">open</a> <a id="241" class="Keyword">import</a> <a id="248" href="Calf.Phase.Closed.html" class="Module">Calf.Phase.Closed</a>
<a id="266" class="Keyword">open</a> <a id="271" class="Keyword">import</a> <a id="278" href="Calf.Data.Equality.html" class="Module">Calf.Data.Equality</a>

<a id="298" class="Keyword">open</a> <a id="303" class="Keyword">import</a> <a id="310" href="Data.Product.html" class="Module">Data.Product</a>
<a id="323" class="Keyword">open</a> <a id="328" class="Keyword">import</a> <a id="335" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="373" class="Symbol">as</a> <a id="376" class="Module">Eq</a> <a id="379" class="Keyword">using</a> <a id="385" class="Symbol">(</a><a id="386" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="389" class="Symbol">;</a> <a id="391" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="395" class="Symbol">;</a> <a id="397" href="Relation.Binary.PropositionalEquality.Core.html#1808" class="Function">subst</a><a id="402" class="Symbol">;</a> <a id="404" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">cong</a><a id="408" class="Symbol">;</a> <a id="410" class="Keyword">module</a> <a id="417" href="Relation.Binary.PropositionalEquality.Properties.html#6744" class="Module">≡-Reasoning</a><a id="428" class="Symbol">)</a>


<a id="unique"></a><a id="432" href="Calf.Phase.Noninterference.html#432" class="Function">unique</a> <a id="439" class="Symbol">:</a> <a id="441" class="Symbol">∀</a> <a id="443" class="Symbol">{</a><a id="444" href="Calf.Phase.Noninterference.html#444" class="Bound">A</a><a id="445" class="Symbol">}</a> <a id="447" class="Symbol">→</a> <a id="449" class="Symbol">(</a><a id="450" href="Calf.Phase.Noninterference.html#450" class="Bound">a</a> <a id="452" class="Symbol">:</a> <a id="454" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="458" class="Symbol">(</a><a id="459" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="461" href="Calf.Phase.Noninterference.html#444" class="Bound">A</a><a id="462" class="Symbol">))</a> <a id="465" class="Symbol">→</a> <a id="467" class="Symbol">(</a><a id="468" href="Calf.Phase.Noninterference.html#468" class="Bound">u</a> <a id="470" class="Symbol">:</a> <a id="472" href="Calf.Phase.Core.html#163" class="Postulate">ext</a><a id="475" class="Symbol">)</a> <a id="477" class="Symbol">→</a> <a id="479" href="Calf.Phase.Noninterference.html#450" class="Bound">a</a> <a id="481" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="483" href="Calf.Phase.Closed.html#416" class="Postulate">∗</a> <a id="485" href="Calf.Phase.Noninterference.html#468" class="Bound">u</a>
<a id="487" href="Calf.Phase.Noninterference.html#432" class="Function">unique</a> <a id="494" class="Symbol">{</a><a id="495" href="Calf.Phase.Noninterference.html#495" class="Bound">A</a><a id="496" class="Symbol">}</a> <a id="498" href="Calf.Phase.Noninterference.html#498" class="Bound">a</a> <a id="500" href="Calf.Phase.Noninterference.html#500" class="Bound">u</a> <a id="502" class="Symbol">=</a>
  <a id="506" href="Calf.Phase.Closed.html#541" class="Postulate">●/ind</a> <a id="512" href="Calf.Phase.Noninterference.html#498" class="Bound">a</a>
    <a id="518" class="Symbol">(λ</a> <a id="521" href="Calf.Phase.Noninterference.html#521" class="Bound">a</a> <a id="523" class="Symbol">→</a> <a id="525" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="529" class="Symbol">(</a><a id="530" href="Calf.Phase.Noninterference.html#521" class="Bound">a</a> <a id="532" href="Calf.Data.Equality.html#240" class="Function">≡⁺[</a> <a id="536" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="538" href="Calf.Phase.Noninterference.html#495" class="Bound">A</a> <a id="540" href="Calf.Data.Equality.html#240" class="Function">]</a> <a id="542" href="Calf.Phase.Closed.html#416" class="Postulate">∗</a> <a id="544" href="Calf.Phase.Noninterference.html#500" class="Bound">u</a><a id="545" class="Symbol">))</a>
    <a id="552" class="Symbol">(λ</a> <a id="555" href="Calf.Phase.Noninterference.html#555" class="Bound">a</a> <a id="557" class="Symbol">→</a> <a id="559" href="Calf.Phase.Closed.html#438" class="Postulate">η≡∗</a> <a id="563" href="Calf.Phase.Noninterference.html#555" class="Bound">a</a> <a id="565" href="Calf.Phase.Noninterference.html#500" class="Bound">u</a><a id="566" class="Symbol">)</a>
    <a id="572" class="Symbol">(λ</a> <a id="575" href="Calf.Phase.Noninterference.html#575" class="Bound">u</a> <a id="577" class="Symbol">→</a> <a id="579" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="583" class="Symbol">)</a>
    <a id="589" class="Symbol">(λ</a> <a id="592" href="Calf.Phase.Noninterference.html#592" class="Bound">a</a> <a id="594" href="Calf.Phase.Noninterference.html#594" class="Bound">u</a> <a id="596" class="Symbol">→</a> <a id="598" href="Calf.Phase.Closed.html#484" class="Postulate">η≡∗/uni</a> <a id="606" class="Symbol">(</a><a id="607" href="Relation.Binary.PropositionalEquality.Core.html#1808" class="Function">subst</a> <a id="613" class="Symbol">(λ</a> <a id="616" href="Calf.Phase.Noninterference.html#616" class="Bound">a₂</a> <a id="619" class="Symbol">→</a> <a id="621" href="Calf.Phase.Noninterference.html#616" class="Bound">a₂</a> <a id="624" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="626" href="Calf.Phase.Closed.html#416" class="Postulate">∗</a> <a id="628" href="Calf.Phase.Noninterference.html#594" class="Bound">u</a><a id="629" class="Symbol">)</a> <a id="631" class="Symbol">(</a><a id="632" href="Calf.Phase.Closed.html#438" class="Postulate">η≡∗</a> <a id="636" href="Calf.Phase.Noninterference.html#592" class="Bound">a</a> <a id="638" href="Calf.Phase.Noninterference.html#594" class="Bound">u</a><a id="639" class="Symbol">)</a> <a id="641" class="Symbol">(</a><a id="642" href="Calf.Phase.Closed.html#438" class="Postulate">η≡∗</a> <a id="646" href="Calf.Phase.Noninterference.html#592" class="Bound">a</a> <a id="648" href="Calf.Phase.Noninterference.html#594" class="Bound">u</a><a id="649" class="Symbol">))</a> <a id="652" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="656" class="Symbol">)</a>

<a id="constant"></a><a id="659" href="Calf.Phase.Noninterference.html#659" class="Function">constant</a> <a id="668" class="Symbol">:</a> <a id="670" class="Symbol">∀</a> <a id="672" class="Symbol">{</a><a id="673" href="Calf.Phase.Noninterference.html#673" class="Bound">A</a> <a id="675" href="Calf.Phase.Noninterference.html#675" class="Bound">B</a><a id="676" class="Symbol">}</a> <a id="678" class="Symbol">(</a><a id="679" href="Calf.Phase.Noninterference.html#679" class="Bound">f</a> <a id="681" class="Symbol">:</a> <a id="683" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="687" class="Symbol">(</a><a id="688" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="690" href="Calf.Phase.Noninterference.html#673" class="Bound">A</a><a id="691" class="Symbol">)</a> <a id="693" class="Symbol">→</a> <a id="695" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="699" class="Symbol">(</a><a id="700" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="703" href="Calf.Phase.Noninterference.html#675" class="Bound">B</a><a id="704" class="Symbol">))</a> <a id="707" class="Symbol">→</a>
  <a id="711" href="Agda.Builtin.Sigma.html#148" class="Record">Σ</a> <a id="713" class="Symbol">(</a><a id="714" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="718" class="Symbol">(</a><a id="719" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="722" href="Calf.Phase.Noninterference.html#675" class="Bound">B</a><a id="723" class="Symbol">))</a> <a id="726" class="Symbol">λ</a> <a id="728" href="Calf.Phase.Noninterference.html#728" class="Bound">b</a> <a id="730" class="Symbol">→</a> <a id="732" href="Calf.Phase.Noninterference.html#679" class="Bound">f</a> <a id="734" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="736" class="Symbol">λ</a> <a id="738" href="Calf.Phase.Noninterference.html#738" class="Bound">_</a> <a id="740" class="Symbol">→</a> <a id="742" href="Calf.Phase.Noninterference.html#728" class="Bound">b</a>
<a id="744" href="Calf.Phase.Noninterference.html#659" class="Function">constant</a> <a id="753" href="Calf.Phase.Noninterference.html#753" class="Bound">f</a> <a id="755" class="Symbol">=</a>
  <a id="759" class="Symbol">(λ</a> <a id="762" href="Calf.Phase.Noninterference.html#762" class="Bound">u</a> <a id="764" class="Symbol">→</a> <a id="766" href="Calf.Phase.Noninterference.html#753" class="Bound">f</a> <a id="768" class="Symbol">(</a><a id="769" href="Calf.Phase.Closed.html#416" class="Postulate">∗</a> <a id="771" href="Calf.Phase.Noninterference.html#762" class="Bound">u</a><a id="772" class="Symbol">)</a> <a id="774" href="Calf.Phase.Noninterference.html#762" class="Bound">u</a><a id="775" class="Symbol">)</a> <a id="777" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="779" href="Calf.Prelude.html#171" class="Postulate">funext</a> <a id="786" class="Symbol">(λ</a> <a id="789" href="Calf.Phase.Noninterference.html#789" class="Bound">a</a> <a id="791" class="Symbol">→</a> <a id="793" href="Calf.Prelude.html#268" class="Postulate">funext/Ω</a> <a id="802" class="Symbol">(λ</a> <a id="805" href="Calf.Phase.Noninterference.html#805" class="Bound">u</a> <a id="807" class="Symbol">→</a>
    <a id="813" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">cong</a> <a id="818" class="Symbol">(λ</a> <a id="821" href="Calf.Phase.Noninterference.html#821" class="Bound">a</a> <a id="823" class="Symbol">→</a> <a id="825" href="Calf.Phase.Noninterference.html#753" class="Bound">f</a> <a id="827" href="Calf.Phase.Noninterference.html#821" class="Bound">a</a> <a id="829" href="Calf.Phase.Noninterference.html#805" class="Bound">u</a><a id="830" class="Symbol">)</a> <a id="832" class="Symbol">(</a><a id="833" href="Calf.Phase.Noninterference.html#432" class="Function">unique</a> <a id="840" href="Calf.Phase.Noninterference.html#789" class="Bound">a</a> <a id="842" href="Calf.Phase.Noninterference.html#805" class="Bound">u</a><a id="843" class="Symbol">)))</a>

<a id="optimization"></a><a id="848" href="Calf.Phase.Noninterference.html#848" class="Function">optimization</a> <a id="861" class="Symbol">:</a> <a id="863" class="Symbol">∀</a> <a id="865" class="Symbol">{</a><a id="866" href="Calf.Phase.Noninterference.html#866" class="Bound">C</a> <a id="868" href="Calf.Phase.Noninterference.html#868" class="Bound">B</a> <a id="870" class="Symbol">:</a> <a id="872" href="Calf.CBPV.html#376" class="Postulate">tp⁺</a><a id="875" class="Symbol">}</a> <a id="877" class="Symbol">{</a><a id="878" href="Calf.Phase.Noninterference.html#878" class="Bound">A</a> <a id="880" class="Symbol">:</a> <a id="882" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="886" href="Calf.Phase.Noninterference.html#866" class="Bound">C</a> <a id="888" class="Symbol">→</a> <a id="890" href="Calf.CBPV.html#376" class="Postulate">tp⁺</a><a id="893" class="Symbol">}</a>
  <a id="897" class="Symbol">(</a><a id="898" href="Calf.Phase.Noninterference.html#898" class="Bound">f</a> <a id="900" class="Symbol">:</a> <a id="902" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="906" class="Symbol">(</a><a id="907" href="Calf.CBPV.html#818" class="Function">Σ⁺</a> <a id="910" href="Calf.Phase.Noninterference.html#866" class="Bound">C</a> <a id="912" class="Symbol">λ</a> <a id="914" href="Calf.Phase.Noninterference.html#914" class="Bound">c</a> <a id="916" class="Symbol">→</a> <a id="918" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="920" class="Symbol">(</a><a id="921" href="Calf.Phase.Noninterference.html#878" class="Bound">A</a> <a id="923" href="Calf.Phase.Noninterference.html#914" class="Bound">c</a><a id="924" class="Symbol">))</a> <a id="927" class="Symbol">→</a> <a id="929" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="933" class="Symbol">(</a><a id="934" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="937" href="Calf.Phase.Noninterference.html#868" class="Bound">B</a><a id="938" class="Symbol">))</a> <a id="941" class="Symbol">→</a>
  <a id="945" href="Agda.Builtin.Sigma.html#148" class="Record">Σ</a> <a id="947" class="Symbol">(</a><a id="948" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="952" href="Calf.Phase.Noninterference.html#866" class="Bound">C</a> <a id="954" class="Symbol">→</a> <a id="956" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="960" class="Symbol">(</a><a id="961" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="964" href="Calf.Phase.Noninterference.html#868" class="Bound">B</a><a id="965" class="Symbol">))</a> <a id="968" class="Symbol">λ</a> <a id="970" href="Calf.Phase.Noninterference.html#970" class="Bound">f&#39;</a> <a id="973" class="Symbol">→</a> <a id="975" class="Symbol">∀</a> <a id="977" href="Calf.Phase.Noninterference.html#977" class="Bound">c</a> <a id="979" href="Calf.Phase.Noninterference.html#979" class="Bound">a</a> <a id="981" class="Symbol">→</a> <a id="983" href="Calf.Phase.Noninterference.html#898" class="Bound">f</a> <a id="985" class="Symbol">(</a><a id="986" href="Calf.Phase.Noninterference.html#977" class="Bound">c</a> <a id="988" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="990" href="Calf.Phase.Noninterference.html#979" class="Bound">a</a><a id="991" class="Symbol">)</a> <a id="993" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="995" href="Calf.Phase.Noninterference.html#970" class="Bound">f&#39;</a> <a id="998" href="Calf.Phase.Noninterference.html#977" class="Bound">c</a>
<a id="1000" href="Calf.Phase.Noninterference.html#848" class="Function">optimization</a> <a id="1013" class="Symbol">{</a><a id="1014" href="Calf.Phase.Noninterference.html#1014" class="Bound">C</a><a id="1015" class="Symbol">}</a> <a id="1017" class="Symbol">{</a><a id="1018" href="Calf.Phase.Noninterference.html#1018" class="Bound">B</a><a id="1019" class="Symbol">}</a> <a id="1021" class="Symbol">{</a><a id="1022" href="Calf.Phase.Noninterference.html#1022" class="Bound">A</a><a id="1023" class="Symbol">}</a> <a id="1025" href="Calf.Phase.Noninterference.html#1025" class="Bound">f</a> <a id="1027" class="Symbol">=</a>
  <a id="1031" class="Symbol">(λ</a> <a id="1034" href="Calf.Phase.Noninterference.html#1034" class="Bound">c</a> <a id="1036" class="Symbol">→</a>
    <a id="1042" class="Keyword">let</a> <a id="1046" href="Calf.Phase.Noninterference.html#1046" class="Bound">g</a> <a id="1048" class="Symbol">:</a> <a id="1050" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="1054" class="Symbol">(</a><a id="1055" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="1057" class="Symbol">(</a><a id="1058" href="Calf.Phase.Noninterference.html#1022" class="Bound">A</a> <a id="1060" href="Calf.Phase.Noninterference.html#1034" class="Bound">c</a><a id="1061" class="Symbol">))</a> <a id="1064" class="Symbol">→</a> <a id="1066" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="1070" class="Symbol">(</a><a id="1071" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="1074" href="Calf.Phase.Noninterference.html#1018" class="Bound">B</a><a id="1075" class="Symbol">)</a>
        <a id="1085" href="Calf.Phase.Noninterference.html#1046" class="Bound">g</a> <a id="1087" href="Calf.Phase.Noninterference.html#1087" class="Bound">a</a> <a id="1089" class="Symbol">=</a> <a id="1091" href="Calf.Phase.Noninterference.html#1025" class="Bound">f</a> <a id="1093" class="Symbol">(</a><a id="1094" href="Calf.Phase.Noninterference.html#1034" class="Bound">c</a> <a id="1096" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1098" href="Calf.Phase.Noninterference.html#1087" class="Bound">a</a><a id="1099" class="Symbol">)</a> <a id="1101" class="Keyword">in</a>
    <a id="1108" class="Keyword">let</a> <a id="1112" class="Symbol">(</a><a id="1113" href="Calf.Phase.Noninterference.html#1113" class="Bound">b</a> <a id="1115" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1117" href="Calf.Phase.Noninterference.html#1117" class="Bound">h</a><a id="1118" class="Symbol">)</a> <a id="1120" class="Symbol">=</a> <a id="1122" href="Calf.Phase.Noninterference.html#659" class="Function">constant</a> <a id="1131" class="Symbol">{</a><a id="1132" href="Calf.Phase.Noninterference.html#1022" class="Bound">A</a> <a id="1134" href="Calf.Phase.Noninterference.html#1034" class="Bound">c</a><a id="1135" class="Symbol">}</a> <a id="1137" class="Symbol">{</a><a id="1138" href="Calf.Phase.Noninterference.html#1018" class="Bound">B</a><a id="1139" class="Symbol">}</a> <a id="1141" href="Calf.Phase.Noninterference.html#1046" class="Bound">g</a> <a id="1143" class="Keyword">in</a>
    <a id="1150" href="Calf.Phase.Noninterference.html#1113" class="Bound">b</a><a id="1151" class="Symbol">)</a> <a id="1153" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a>
    <a id="1159" class="Symbol">λ</a> <a id="1161" href="Calf.Phase.Noninterference.html#1161" class="Bound">c</a> <a id="1163" href="Calf.Phase.Noninterference.html#1163" class="Bound">a</a> <a id="1165" class="Symbol">→</a>
    <a id="1171" class="Keyword">let</a> <a id="1175" href="Calf.Phase.Noninterference.html#1175" class="Bound">g</a> <a id="1177" class="Symbol">:</a> <a id="1179" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="1183" class="Symbol">(</a><a id="1184" href="Calf.Phase.Closed.html#375" class="Postulate">●</a> <a id="1186" class="Symbol">(</a><a id="1187" href="Calf.Phase.Noninterference.html#1022" class="Bound">A</a> <a id="1189" href="Calf.Phase.Noninterference.html#1161" class="Bound">c</a><a id="1190" class="Symbol">))</a> <a id="1193" class="Symbol">→</a> <a id="1195" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="1199" class="Symbol">(</a><a id="1200" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="1203" href="Calf.Phase.Noninterference.html#1018" class="Bound">B</a><a id="1204" class="Symbol">)</a>
        <a id="1214" href="Calf.Phase.Noninterference.html#1175" class="Bound">g</a> <a id="1216" href="Calf.Phase.Noninterference.html#1216" class="Bound">a</a> <a id="1218" class="Symbol">=</a> <a id="1220" href="Calf.Phase.Noninterference.html#1025" class="Bound">f</a> <a id="1222" class="Symbol">(</a><a id="1223" href="Calf.Phase.Noninterference.html#1161" class="Bound">c</a> <a id="1225" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1227" href="Calf.Phase.Noninterference.html#1216" class="Bound">a</a><a id="1228" class="Symbol">)</a> <a id="1230" class="Keyword">in</a>
    <a id="1237" class="Keyword">let</a> <a id="1241" class="Symbol">(</a><a id="1242" href="Calf.Phase.Noninterference.html#1242" class="Bound">b</a> <a id="1244" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1246" href="Calf.Phase.Noninterference.html#1246" class="Bound">h</a><a id="1247" class="Symbol">)</a> <a id="1249" class="Symbol">=</a> <a id="1251" href="Calf.Phase.Noninterference.html#659" class="Function">constant</a> <a id="1260" class="Symbol">{</a><a id="1261" href="Calf.Phase.Noninterference.html#1022" class="Bound">A</a> <a id="1263" href="Calf.Phase.Noninterference.html#1161" class="Bound">c</a><a id="1264" class="Symbol">}</a> <a id="1266" class="Symbol">{</a><a id="1267" href="Calf.Phase.Noninterference.html#1018" class="Bound">B</a><a id="1268" class="Symbol">}</a> <a id="1270" href="Calf.Phase.Noninterference.html#1175" class="Bound">g</a> <a id="1272" class="Keyword">in</a>
    <a id="1279" href="Relation.Binary.PropositionalEquality.Core.html#1489" class="Function">Eq.cong-app</a> <a id="1291" href="Calf.Phase.Noninterference.html#1246" class="Bound">h</a> <a id="1293" href="Calf.Phase.Noninterference.html#1163" class="Bound">a</a>

<a id="1296" class="Keyword">module</a> <a id="1303" href="Calf.Phase.Noninterference.html#1303" class="Module">_</a> <a id="1305" class="Symbol">(</a><a id="1306" href="Calf.Phase.Noninterference.html#1306" class="Bound">costMonoid</a> <a id="1317" class="Symbol">:</a> <a id="1319" href="Algebra.Cost.Bundles.html#332" class="Record">CostMonoid</a><a id="1329" class="Symbol">)</a> <a id="1331" class="Keyword">where</a>
  <a id="1339" class="Keyword">open</a> <a id="1344" class="Keyword">import</a> <a id="1351" href="Calf.Step.html" class="Module">Calf.Step</a> <a id="1361" href="Calf.Phase.Noninterference.html#1306" class="Bound">costMonoid</a>

  <a id="1375" href="Calf.Phase.Noninterference.html#1375" class="Function">oblivious</a> <a id="1385" class="Symbol">:</a> <a id="1387" class="Symbol">∀</a> <a id="1389" class="Symbol">{</a><a id="1390" href="Calf.Phase.Noninterference.html#1390" class="Bound">A</a> <a id="1392" href="Calf.Phase.Noninterference.html#1392" class="Bound">B</a><a id="1393" class="Symbol">}</a> <a id="1395" class="Symbol">(</a><a id="1396" href="Calf.Phase.Noninterference.html#1396" class="Bound">f</a> <a id="1398" class="Symbol">:</a> <a id="1400" href="Calf.CBPV.html#595" class="Function">cmp</a> <a id="1404" class="Symbol">(</a><a id="1405" href="Calf.CBPV.html#413" class="Postulate">F</a> <a id="1407" href="Calf.Phase.Noninterference.html#1390" class="Bound">A</a><a id="1408" class="Symbol">)</a> <a id="1410" class="Symbol">→</a> <a id="1412" href="Calf.CBPV.html#396" class="Postulate">val</a> <a id="1416" class="Symbol">(</a><a id="1417" href="Calf.Phase.Open.html#555" class="Function Operator">◯⁺</a> <a id="1420" href="Calf.Phase.Noninterference.html#1392" class="Bound">B</a><a id="1421" class="Symbol">))</a> <a id="1424" class="Symbol">→</a>
        <a id="1434" class="Symbol">∀</a> <a id="1436" href="Calf.Phase.Noninterference.html#1436" class="Bound">c</a> <a id="1438" href="Calf.Phase.Noninterference.html#1438" class="Bound">e</a> <a id="1440" class="Symbol">→</a> <a id="1442" href="Calf.Phase.Noninterference.html#1396" class="Bound">f</a> <a id="1444" class="Symbol">(</a><a id="1445" href="Calf.Step.html#440" class="Function">step</a> <a id="1450" class="Symbol">(</a><a id="1451" href="Calf.CBPV.html#413" class="Postulate">F</a> <a id="1453" href="Calf.Phase.Noninterference.html#1390" class="Bound">A</a><a id="1454" class="Symbol">)</a> <a id="1456" href="Calf.Phase.Noninterference.html#1436" class="Bound">c</a> <a id="1458" href="Calf.Phase.Noninterference.html#1438" class="Bound">e</a><a id="1459" class="Symbol">)</a> <a id="1461" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1463" href="Calf.Phase.Noninterference.html#1396" class="Bound">f</a> <a id="1465" href="Calf.Phase.Noninterference.html#1438" class="Bound">e</a>
  <a id="1469" href="Calf.Phase.Noninterference.html#1375" class="Function">oblivious</a> <a id="1479" class="Symbol">{</a><a id="1480" href="Calf.Phase.Noninterference.html#1480" class="Bound">A</a><a id="1481" class="Symbol">}</a> <a id="1483" class="Symbol">{</a><a id="1484" href="Calf.Phase.Noninterference.html#1484" class="Bound">B</a><a id="1485" class="Symbol">}</a> <a id="1487" href="Calf.Phase.Noninterference.html#1487" class="Bound">f</a> <a id="1489" href="Calf.Phase.Noninterference.html#1489" class="Bound">c</a> <a id="1491" href="Calf.Phase.Noninterference.html#1491" class="Bound">e</a> <a id="1493" class="Symbol">=</a> <a id="1495" href="Calf.Prelude.html#268" class="Postulate">funext/Ω</a> <a id="1504" class="Symbol">λ</a> <a id="1506" href="Calf.Phase.Noninterference.html#1506" class="Bound">u</a> <a id="1508" class="Symbol">→</a>
    <a id="1514" href="Relation.Binary.Reasoning.Syntax.html#1510" class="Function Operator">begin</a>
      <a id="1526" href="Calf.Phase.Noninterference.html#1487" class="Bound">f</a> <a id="1528" class="Symbol">(</a><a id="1529" href="Calf.Step.html#440" class="Function">step</a> <a id="1534" class="Symbol">(</a><a id="1535" href="Calf.CBPV.html#413" class="Postulate">F</a> <a id="1537" href="Calf.Phase.Noninterference.html#1480" class="Bound">A</a><a id="1538" class="Symbol">)</a> <a id="1540" href="Calf.Phase.Noninterference.html#1489" class="Bound">c</a> <a id="1542" href="Calf.Phase.Noninterference.html#1491" class="Bound">e</a><a id="1543" class="Symbol">)</a> <a id="1545" href="Calf.Phase.Noninterference.html#1506" class="Bound">u</a>
    <a id="1551" href="Relation.Binary.Reasoning.Syntax.html#10986" class="Function">≡⟨</a> <a id="1554" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">cong</a> <a id="1559" class="Symbol">(λ</a> <a id="1562" href="Calf.Phase.Noninterference.html#1562" class="Bound">e</a> <a id="1564" class="Symbol">→</a> <a id="1566" href="Calf.Phase.Noninterference.html#1487" class="Bound">f</a> <a id="1568" href="Calf.Phase.Noninterference.html#1562" class="Bound">e</a> <a id="1570" href="Calf.Phase.Noninterference.html#1506" class="Bound">u</a><a id="1571" class="Symbol">)</a> <a id="1573" class="Symbol">(</a><a id="1574" href="Calf.Step.html#634" class="Function">step/ext</a> <a id="1583" class="Symbol">(</a><a id="1584" href="Calf.CBPV.html#413" class="Postulate">F</a> <a id="1586" href="Calf.Phase.Noninterference.html#1480" class="Bound">A</a><a id="1587" class="Symbol">)</a> <a id="1589" href="Calf.Phase.Noninterference.html#1491" class="Bound">e</a> <a id="1591" href="Calf.Phase.Noninterference.html#1489" class="Bound">c</a> <a id="1593" href="Calf.Phase.Noninterference.html#1506" class="Bound">u</a><a id="1594" class="Symbol">)</a> <a id="1596" href="Relation.Binary.Reasoning.Syntax.html#10986" class="Function">⟩</a>
      <a id="1604" href="Calf.Phase.Noninterference.html#1487" class="Bound">f</a> <a id="1606" href="Calf.Phase.Noninterference.html#1491" class="Bound">e</a> <a id="1608" href="Calf.Phase.Noninterference.html#1506" class="Bound">u</a>
    <a id="1614" href="Relation.Binary.Reasoning.Syntax.html#12283" class="Function Operator">∎</a>
    <a id="1620" class="Keyword">where</a> <a id="1626" class="Keyword">open</a> <a id="1631" href="Relation.Binary.PropositionalEquality.Properties.html#6744" class="Module">≡-Reasoning</a>
</pre></body></html>