{-# OPTIONS --rewriting #-}

module Examples.Decalf.ProbabilisticChoice where

open import Algebra.Cost

costMonoid = â„•-CostMonoid
open CostMonoid costMonoid using (â„‚)

open import Calf costMonoid
open import Calf.Data.Equality as Eq using (_â‰¡_; module â‰¡-Reasoning)

open import Data.Interval public


postulate
  flip : (X : tpâ») â†’ ğ•€ â†’ cmp X â†’ cmp X â†’ cmp X

  flip/0 : {eâ‚€ eâ‚ : cmp X} â†’
    flip X 0ğ•€ eâ‚€ eâ‚ â‰¡ eâ‚€
  flip/1 : {eâ‚€ eâ‚ : cmp X} â†’
    flip X 1ğ•€ eâ‚€ eâ‚ â‰¡ eâ‚
  {-# REWRITE flip/0 flip/1 #-}
  flip/same : {e : cmp X} {p : ğ•€} â†’
    flip X p e e â‰¡ e

  flip/sym : (X : tpâ») (p : ğ•€) (eâ‚€ eâ‚ : cmp X) â†’
    flip X p eâ‚€ eâ‚ â‰¡ flip X (1- p) eâ‚ eâ‚€
  flip/assocÊ³ : (X : tpâ») (eâ‚€ eâ‚ eâ‚‚ : cmp X) {p q r : ğ•€} â†’ p â‰¡ (p âˆ¨ q) âˆ§ r â†’
    flip X p (flip X q eâ‚€ eâ‚) eâ‚‚ â‰¡ flip X (p âˆ¨ q) eâ‚€ (flip X r eâ‚ eâ‚‚)

flip/assocË¡ : (X : tpâ») (eâ‚€ eâ‚ eâ‚‚ : cmp X) {p q r : ğ•€} â†’ p â‰¡ (p âˆ§ q) âˆ¨ r â†’
  flip X p eâ‚€ (flip X q eâ‚ eâ‚‚) â‰¡ flip X (p âˆ§ q) (flip X r eâ‚€ eâ‚) eâ‚‚
flip/assocË¡ X eâ‚€ eâ‚ eâ‚‚ {p} {q} {r} h =
  let open â‰¡-Reasoning in
  begin
    flip X p eâ‚€ (flip X q eâ‚ eâ‚‚)
  â‰¡âŸ¨ Eq.cong (Î» p â†’ flip X p eâ‚€ (flip X q eâ‚ eâ‚‚)) h âŸ©
    flip X (p âˆ§ q âˆ¨ r) eâ‚€ (flip X q eâ‚ eâ‚‚)
  â‰¡Ë˜âŸ¨ flip/assocÊ³ X eâ‚€ eâ‚ eâ‚‚ (Eq.cong (_âˆ§ q) h) âŸ©
    flip X (p âˆ§ q) (flip X r eâ‚€ eâ‚) eâ‚‚
  âˆ

postulate
  bind/flip : {f : val A â†’ cmp X} {p : ğ•€} {eâ‚€ eâ‚ : cmp (F A)} â†’
    bind {A = A} X (flip (F A) p eâ‚€ eâ‚) f â‰¡ flip X p (bind X eâ‚€ f) (bind X eâ‚ f)
  {-# REWRITE bind/flip #-}

  Î /flip : {X : val A â†’ tpâ»} {p : ğ•€} {eâ‚€ eâ‚ : cmp (Î  A X)} â†’
    flip (Î  A X) p eâ‚€ eâ‚ â‰¡ Î» a â†’ flip (X a) p (eâ‚€ a) (eâ‚ a)
  {-# REWRITE Î /flip #-}

  flip/step : {eâ‚€ eâ‚ : cmp X} {p : ğ•€} â†’
    step X c (flip X p eâ‚€ eâ‚) â‰¡ flip X p (step X c eâ‚€) (step X c eâ‚)
  {-# REWRITE flip/step #-}
