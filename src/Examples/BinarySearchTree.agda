{-# OPTIONS --prop --rewriting #-}

module Examples.BinarySearchTree where

open import Calf.CostMonoid
open import Calf.CostMonoids using (â„•Â²-ParCostMonoid)

parCostMonoid = â„•Â²-ParCostMonoid
open ParCostMonoid parCostMonoid

open import Level using (0â„“)

open import Calf costMonoid
open import Calf.ParMetalanguage parCostMonoid
open import Calf.Types.Unit
open import Calf.Types.Product
open import Calf.Types.Sum
open import Calf.Types.Bool
open import Calf.Types.Maybe
open import Calf.Types.Nat
open import Calf.Types.List
open import Data.String using (String)
open import Data.Nat as Nat using (_+_; _*_; _<_; _>_; _â‰¤áµ‡_; _<áµ‡_; âŒŠ_/2âŒ‹; _â‰¡áµ‡_; _â‰¥_)
open import Data.Bool as Bool using (not; _âˆ§_)
import Data.Nat.Properties as Nat

open import Function

open import Relation.Nullary
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Binary
open import Relation.Binary.PropositionalEquality as Eq using (_â‰¡_; refl; _â‰¢_; module â‰¡-Reasoning; â‰¢-sym)


variable
  A B C : tp pos
  X Y Z : tp neg
  P Q : val A â†’ tp neg


-- Middle Sequence
record MSequence (ğ•‚ : tp pos) : Set where
  field
    seq : tp pos

    empty : cmp (F seq)
    join : cmp (Î  seq Î» tâ‚ â†’ Î  ğ•‚ Î» k â†’ Î  seq Î» tâ‚‚ â†’ F seq)

    rec :
      cmp
        ( Î  (U X) Î» _ â†’
          Î  (U (Î  seq Î» _ â†’ Î  (U X) Î» _ â†’ Î  ğ•‚ Î» _ â†’ Î  seq Î» _ â†’ Î  (U X) Î» _ â†’ X)) Î» _ â†’
          Î  seq Î» _ â†’ X
        )


ListMSequence : (ğ•‚ : tp pos) â†’ MSequence ğ•‚
ListMSequence ğ•‚ =
  record
    { seq = list ğ•‚
    ; empty = ret []
    ; join =
        Î» lâ‚ k lâ‚‚ â†’
          let n = length lâ‚ + 1 + length lâ‚‚ in
          step (F (list ğ•‚)) (n , n) (ret (lâ‚ ++ [ k ] ++ lâ‚‚))
    ; rec = Î» {X} â†’ rec {X}
    }
  where
    rec : {X : tp neg} â†’
      cmp
        ( Î  (U X) Î» _ â†’
          Î  (U (Î  (list ğ•‚) Î» _ â†’ Î  (U X) Î» _ â†’ Î  ğ•‚ Î» _ â†’ Î  (list ğ•‚) Î» _ â†’ Î  (U X) Î» _ â†’ X)) Î» _ â†’
          Î  (list ğ•‚) Î» _ â†’ X
        )
    rec {X} z f []      = z
    rec {X} z f (x âˆ· l) = step X (1 , 1) (f [] z x l (rec {X} z f l))


RedBlackMSequence : (ğ•‚ : tp pos) â†’ MSequence ğ•‚
RedBlackMSequence ğ•‚ =
  record
    { seq = rbt
    ; empty = ret âŸª leaf âŸ«
    ; join = join
    ; rec = Î» {X} â†’ rec {X}
    }
  where
    data Color : Set where
      red : Color
      black : Color
    color : tp pos
    color = U (meta Color)

    -- Indexed Red Black Tree
    data IRBT : val color â†’ val nat â†’ Set where
      leaf  : IRBT black zero
      red   : {n : val nat}
        (tâ‚ : IRBT black n) (k : val ğ•‚) (tâ‚‚ : IRBT black n)
        â†’ IRBT red n
      black : {n : val nat} {yâ‚ yâ‚‚ : val color}
        (tâ‚ : IRBT yâ‚ n) (k : val ğ•‚) (tâ‚‚ : IRBT yâ‚‚ n)
        â†’ IRBT black (suc n)
    irbt : val color â†’ val nat â†’ tp pos
    irbt y n = U (meta (IRBT y n))

    record RBT : Set where
      pattern
      constructor âŸª_âŸ«
      field
        {y} : val color
        {n} : val nat
        t : val (irbt y n)
    rbt : tp pos
    rbt = U (meta RBT)


    data AlmostLeftRBT : (right-color : val color) â†’ val nat â†’ Set where
      violation :
        {n : val nat}
        â†’ IRBT red n â†’ val ğ•‚ â†’ IRBT black n
        â†’ AlmostLeftRBT red n
      valid :
        {right-color : val color} {n : val nat} {y : val color} â†’ IRBT y n
        â†’ AlmostLeftRBT right-color n
    alrbt : val color â†’ val nat â†’ tp pos
    alrbt y n = U (meta (AlmostLeftRBT y n))

    joinLeft :
      cmp
        ( Î  color Î» yâ‚ â†’ Î  nat Î» nâ‚ â†’ Î  (irbt yâ‚ nâ‚) Î» _ â†’
          Î  ğ•‚ Î» _ â†’
          Î  color Î» yâ‚‚ â†’ Î  nat Î» nâ‚‚ â†’ Î  (irbt yâ‚‚ nâ‚‚) Î» _ â†’
          Î  (U (meta (nâ‚ < nâ‚‚))) Î» _ â†’
          F (alrbt yâ‚‚ nâ‚‚)
        )
    joinLeft yâ‚ nâ‚ tâ‚ k .red nâ‚‚ (red tâ‚‚â‚ kâ‚ tâ‚‚â‚‚) nâ‚<nâ‚‚ =
      bind (F (alrbt red nâ‚‚)) (joinLeft _ _ tâ‚ k _ _ tâ‚‚â‚ nâ‚<nâ‚‚) Î»
        { (valid {y = red} t') â†’ ret (violation t' kâ‚ tâ‚‚â‚‚)
        ; (valid {y = black} t') â†’ ret (valid (red t' kâ‚ tâ‚‚â‚‚)) }
    joinLeft yâ‚ nâ‚ tâ‚ k .black (suc nâ‚‚) (black {yâ‚ = c} tâ‚‚â‚ kâ‚ tâ‚‚â‚‚) nâ‚<nâ‚‚ with nâ‚ Nat.â‰Ÿ nâ‚‚
    joinLeft red nâ‚ (red tâ‚â‚ kâ‚ tâ‚â‚‚) k .black (suc nâ‚) (black tâ‚‚â‚ kâ‚‚ tâ‚‚â‚‚) nâ‚<nâ‚‚ | yes refl =
      ret (valid (red (black tâ‚â‚ kâ‚ tâ‚â‚‚) k (black tâ‚‚â‚ kâ‚‚ tâ‚‚â‚‚)))
    joinLeft black nâ‚ tâ‚ k .black (suc nâ‚) (black {yâ‚ = red} (red tâ‚‚â‚â‚ kâ‚â‚ tâ‚‚â‚â‚‚) kâ‚ tâ‚‚â‚‚) nâ‚<nâ‚‚ | yes refl =
      ret (valid (red (black tâ‚ k tâ‚‚â‚â‚) kâ‚â‚ (black tâ‚‚â‚â‚‚ kâ‚ tâ‚‚â‚‚)))
    joinLeft black nâ‚ tâ‚ k .black (suc nâ‚) (black {yâ‚ = black} tâ‚‚â‚ kâ‚ tâ‚‚â‚‚) nâ‚<nâ‚‚ | yes refl =
      ret (valid (black (red tâ‚ k tâ‚‚â‚) kâ‚ tâ‚‚â‚‚))
    ... | no nâ‚â‰¢nâ‚‚ =
      bind (F (alrbt black (suc nâ‚‚))) (joinLeft _ _ tâ‚ k _ _ tâ‚‚â‚ (Nat.â‰¤âˆ§â‰¢â‡’< (Nat.â‰¤-pred nâ‚<nâ‚‚) nâ‚â‰¢nâ‚‚)) Î»
        { (violation (red t'â‚â‚ k'â‚ t'â‚â‚‚) k' t'â‚‚) â†’ ret (valid (red (black t'â‚â‚ k'â‚ t'â‚â‚‚) k' (black t'â‚‚ kâ‚ tâ‚‚â‚‚)))
        ; (valid t') â†’ ret (valid (black t' kâ‚ tâ‚‚â‚‚)) }

    data AlmostRightRBT : (left-color : val color) â†’ val nat â†’ Set where
      violation :
        {n : val nat}
        â†’ IRBT black n â†’ val ğ•‚ â†’ IRBT red n
        â†’ AlmostRightRBT red n
      valid :
        {left-color : val color} {n : val nat} {y : val color} â†’ IRBT y n
        â†’ AlmostRightRBT left-color n
    arrbt : val color â†’ val nat â†’ tp pos
    arrbt y n = U (meta (AlmostRightRBT y n))

    joinRight :
      cmp
        ( Î  color Î» yâ‚ â†’ Î  nat Î» nâ‚ â†’ Î  (irbt yâ‚ nâ‚) Î» _ â†’
          Î  ğ•‚ Î» _ â†’
          Î  color Î» yâ‚‚ â†’ Î  nat Î» nâ‚‚ â†’ Î  (irbt yâ‚‚ nâ‚‚) Î» _ â†’
          Î  (U (meta (nâ‚ > nâ‚‚))) Î» _ â†’
          F (arrbt yâ‚ nâ‚)
        )
    joinRight .red nâ‚ (red tâ‚â‚ kâ‚ tâ‚â‚‚) k yâ‚‚ nâ‚‚ tâ‚‚ nâ‚>nâ‚‚ =
      bind (F (arrbt red nâ‚)) (joinRight _ _ tâ‚â‚‚ k _ _ tâ‚‚ nâ‚>nâ‚‚) Î»
        { (valid {y = red} t') â†’ ret (violation tâ‚â‚ kâ‚ t')
        ; (valid {y = black} t') â†’ ret (valid (red tâ‚â‚ kâ‚ t')) }
    joinRight .black (suc nâ‚) (black tâ‚â‚ kâ‚ tâ‚â‚‚) k yâ‚‚ nâ‚‚ tâ‚‚ nâ‚>nâ‚‚ with nâ‚ Nat.â‰Ÿ nâ‚‚
    joinRight .black (suc nâ‚) (black tâ‚â‚ kâ‚ tâ‚â‚‚) k red nâ‚ (red tâ‚‚â‚ kâ‚‚ tâ‚‚â‚‚) nâ‚>nâ‚‚ | yes refl =
      ret (valid (red (black tâ‚â‚ kâ‚ tâ‚â‚‚) k (black tâ‚‚â‚ kâ‚‚ tâ‚‚â‚‚)))
    joinRight .black (suc nâ‚) (black {yâ‚‚ = red} tâ‚â‚ kâ‚ (red tâ‚â‚‚â‚ kâ‚â‚‚ tâ‚â‚‚â‚‚)) k black nâ‚ tâ‚‚ nâ‚>nâ‚‚ | yes refl =
      ret (valid (red (black tâ‚â‚ kâ‚ tâ‚â‚‚â‚) kâ‚â‚‚ (black tâ‚â‚‚â‚‚ k tâ‚‚)))
    joinRight .black (suc nâ‚) (black {yâ‚‚ = black} tâ‚â‚ kâ‚ tâ‚â‚‚) k black nâ‚ tâ‚‚ nâ‚>nâ‚‚ | yes refl =
      ret (valid (black tâ‚â‚ kâ‚ (red tâ‚â‚‚ k tâ‚‚)))
    ... | no nâ‚â‰¢nâ‚‚ =
      bind (F (arrbt black (suc nâ‚))) (joinRight _ _ tâ‚â‚‚ k _ _ tâ‚‚ (Nat.â‰¤âˆ§â‰¢â‡’< (Nat.â‰¤-pred nâ‚>nâ‚‚) (â‰¢-sym nâ‚â‰¢nâ‚‚))) Î»
        { (violation t'â‚ k' (red t'â‚‚â‚ k'â‚‚ t'â‚‚â‚‚)) â†’ ret (valid (red (black tâ‚â‚ kâ‚ t'â‚) k' (black t'â‚‚â‚ k'â‚‚ t'â‚‚â‚‚)))
        ; (valid t') â†’ ret (valid (black tâ‚â‚ kâ‚ t'))  }

    i-join :
      cmp
        ( Î  color Î» yâ‚ â†’ Î  nat Î» nâ‚ â†’ Î  (irbt yâ‚ nâ‚) Î» _ â†’
          Î  ğ•‚ Î» _ â†’
          Î  color Î» yâ‚‚ â†’ Î  nat Î» nâ‚‚ â†’ Î  (irbt yâ‚‚ nâ‚‚) Î» _ â†’
          F rbt
        )
    i-join yâ‚ nâ‚ tâ‚ k yâ‚‚ nâ‚‚ tâ‚‚ with Nat.<-cmp nâ‚ nâ‚‚
    i-join red nâ‚ tâ‚ k yâ‚‚ nâ‚‚ tâ‚‚ | triâ‰ˆ Â¬nâ‚<nâ‚‚ refl Â¬nâ‚>nâ‚‚ = ret âŸª (black tâ‚ k tâ‚‚) âŸ«
    i-join black nâ‚ tâ‚ k red nâ‚‚ tâ‚‚ | triâ‰ˆ Â¬nâ‚<nâ‚‚ refl Â¬nâ‚>nâ‚‚ = ret âŸª (black tâ‚ k tâ‚‚) âŸ«
    i-join black nâ‚ tâ‚ k black nâ‚‚ tâ‚‚ | triâ‰ˆ Â¬nâ‚<nâ‚‚ refl Â¬nâ‚>nâ‚‚ = ret âŸª (red tâ‚ k tâ‚‚) âŸ«
    ... | tri< nâ‚<nâ‚‚ nâ‚â‰¢nâ‚‚ Â¬nâ‚>nâ‚‚ =
      bind (F rbt) (joinLeft _ _ tâ‚ k _ _ tâ‚‚ nâ‚<nâ‚‚) Î»
        { (violation t'â‚ k' t'â‚‚) â†’ ret âŸª (black t'â‚ k' t'â‚‚) âŸ«
        ; (valid t') â†’ ret âŸª t' âŸ«}
    ... | tri> Â¬nâ‚<nâ‚‚ nâ‚â‰¢nâ‚‚ nâ‚>nâ‚‚ =
      bind (F rbt) (joinRight _ _ tâ‚ k _ _ tâ‚‚ nâ‚>nâ‚‚) Î»
        { (violation t'â‚ k' t'â‚‚) â†’ ret âŸª black t'â‚ k' t'â‚‚ âŸ«
        ; (valid t') â†’ ret âŸª t' âŸ« }

    join : cmp (Î  rbt Î» _ â†’ Î  ğ•‚ Î» _ â†’ Î  rbt Î» _ â†’ F rbt)
    join âŸª tâ‚ âŸ« k âŸª tâ‚‚ âŸ« = i-join _ _ tâ‚ k _ _ tâ‚‚


    i-rec : {X : tp neg} â†’
      cmp
        ( Î  (U X) Î» _ â†’
          Î 
            ( U
              ( Î  color Î» yâ‚ â†’ Î  nat Î» nâ‚ â†’ Î  (irbt yâ‚ nâ‚) Î» _ â†’ Î  (U X) Î» _ â†’
                Î  ğ•‚ Î» _ â†’
                Î  color Î» yâ‚‚ â†’ Î  nat Î» nâ‚‚ â†’ Î  (irbt yâ‚‚ nâ‚‚) Î» _ â†’ Î  (U X) Î» _ â†’
                X
              )
            ) Î» _ â†’
          Î  color Î» y â†’ Î  nat Î» n â†’ Î  (irbt y n) Î» _ â†’
          X
        )
    i-rec {X} z f .black .zero    leaf            = z
    i-rec {X} z f .red   n        (red   tâ‚ k tâ‚‚) =
      f
        _ _ tâ‚ (i-rec {X} z f _ _ tâ‚)
        k
        _ _ tâ‚‚ (i-rec {X} z f _ _ tâ‚‚)
    i-rec {X} z f .black .(suc _) (black tâ‚ k tâ‚‚) =
      f
        _ _ tâ‚ (i-rec {X} z f _ _ tâ‚)
        k
        _ _ tâ‚‚ (i-rec {X} z f _ _ tâ‚‚)

    rec : {X : tp neg} â†’
      cmp
        ( Î  (U X) Î» _ â†’
          Î  (U (Î  rbt Î» _ â†’ Î  (U X) Î» _ â†’ Î  ğ•‚ Î» _ â†’ Î  rbt Î» _ â†’ Î  (U X) Î» _ â†’ X)) Î» _ â†’
          Î  rbt Î» _ â†’ X
        )
    rec {X} z f âŸª t âŸ« =
      i-rec {X}
        z
        (Î» _ _ tâ‚ ihâ‚ k _ _ tâ‚‚ ihâ‚‚ â†’ f âŸª tâ‚ âŸ« ihâ‚ k âŸª tâ‚‚ âŸ« ihâ‚‚)
        _ _ t


module BinarySearchTree
  (Key : StrictTotalOrder 0â„“ 0â„“ 0â„“)
  (MSeq : MSequence (U (meta (StrictTotalOrder.Carrier Key))))
  where

  open StrictTotalOrder Key

  ğ•‚ : tp pos
  ğ•‚ = U (meta (StrictTotalOrder.Carrier Key))

  open MSequence MSeq public

  singleton : cmp (Î  ğ•‚ Î» _ â†’ F seq)
  singleton k =
    bind (F seq) empty Î» t â†’
    join t k t

  Split : tp neg
  Split = F (prodâº seq (prodâº (maybe ğ•‚) seq))

  split : cmp (Î  seq Î» _ â†’ Î  ğ•‚ Î» _ â†’ Split)
  split t k =
    rec
      {X = F (prodâº seq (prodâº (maybe ğ•‚) seq))}
      (bind Split empty Î» t â†’
        ret (t , nothing , t))
      (Î» tâ‚ ihâ‚ k' tâ‚‚ ihâ‚‚ â†’
        case compare k k' of Î»
          { (tri< k<k' Â¬kâ‰¡k' Â¬k>k') â†’
              bind Split ihâ‚ Î» ( tâ‚â‚ , k? , tâ‚â‚‚ ) â†’
              bind Split (join tâ‚â‚‚ k' tâ‚‚) Î» t â†’
              ret (tâ‚â‚ , k? , t)
          ; (triâ‰ˆ Â¬k<k' kâ‰¡k' Â¬k>k') â†’ ret (tâ‚ , just k' , tâ‚‚)
          ; (tri> Â¬k<k' Â¬kâ‰¡k' k>k') â†’
              bind Split ihâ‚‚ Î» ( tâ‚‚â‚ , k? , tâ‚‚â‚‚ ) â†’
              bind Split (join tâ‚ k' tâ‚‚â‚) Î» t â†’
              ret (t , k? , tâ‚‚â‚‚)
          })
      t

  find : cmp (Î  seq Î» _ â†’ Î  ğ•‚ Î» _ â†’ F (maybe ğ•‚))
  find t k = bind (F (maybe ğ•‚)) (split t k) Î» { (_ , k? , _) â†’ ret k? }

  insert : cmp (Î  seq Î» _ â†’ Î  ğ•‚ Î» _ â†’ F seq)
  insert t k = bind (F seq) (split t k) Î» { (tâ‚ , _ , tâ‚‚) â†’ join tâ‚ k tâ‚‚ }


module Ex/NatSet where
  open BinarySearchTree Nat.<-strictTotalOrder (RedBlackMSequence _)

  example : cmp Split
  example =
    bind Split (singleton 1) Î» tâ‚ â†’
    bind Split (insert tâ‚ 2) Î» tâ‚ â†’
    bind Split (singleton 4) Î» tâ‚‚ â†’
    bind Split (join tâ‚ 3 tâ‚‚) Î» t â†’
    split t 2

  -- run Ctrl-C Ctrl-N here
  compute : cmp Split
  compute = {! example  !}


module Ex/NatStringDict where
  strictTotalOrder : StrictTotalOrder 0â„“ 0â„“ 0â„“
  strictTotalOrder =
    record
      { Carrier = â„• Ã— String
      ; _â‰ˆ_ = Î» (nâ‚ , _) (nâ‚‚ , _) â†’ nâ‚ â‰¡ nâ‚‚
      ; _<_ = Î» (nâ‚ , _) (nâ‚‚ , _) â†’ nâ‚ < nâ‚‚
      ; isStrictTotalOrder =
          record
            { isEquivalence =
                record
                  { refl = Eq.refl
                  ; sym = Eq.sym
                  ; trans = Eq.trans
                  }
            ; trans = Nat.<-trans
            ; compare = Î» (nâ‚ , _) (nâ‚‚ , _) â†’ Nat.<-cmp nâ‚ nâ‚‚
            }
      }

  open BinarySearchTree strictTotalOrder (RedBlackMSequence _)

  example : cmp Split
  example =
    bind Split (singleton (1 , "red")) Î» tâ‚ â†’
    bind Split (insert tâ‚ (2 , "orange")) Î» tâ‚ â†’
    bind Split (singleton (4 , "green")) Î» tâ‚‚ â†’
    bind Split (join tâ‚ (3 , "yellow") tâ‚‚) Î» t â†’
    split t (2 , "")

  -- run Ctrl-C Ctrl-N here
  compute : cmp Split
  compute = {! example  !}
